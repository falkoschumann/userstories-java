import org.apache.tools.ant.taskdefs.condition.Os

import java.time.LocalDate

plugins {
  id 'helloworld.java-application-conventions'
  id 'com.gluonhq.client-gradle-plugin'
  id 'org.beryx.jlink'
}

dependencies {
  implementation project(':helloworld-frontend')
  implementation project(':helloworld-backend')
}

def year = LocalDate.now().year
def copyright = "Copyright (c) ${year} Falko Schumann"

processResources {
  filesMatching('**/*.properties') {
    expand(title: 'Hello World', version: rootProject.version, copyright: copyright)
  }
}

application {
  mainModule = 'de.muspellheim.helloworld'
  mainClassName = 'de.muspellheim.helloworld.App'
}

jlink {
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
}

if (Os.isFamily(Os.FAMILY_MAC)) {
  jlink {
    launcher {
      args = ['--useSystemMenuBar']
    }
    jpackage {
      icon = 'src/main/macos/AppIcon.icns'
      imageName = 'Hello World'
      installerType = 'dmg'
      imageOptions = [
        '--copyright', copyright,
        '--mac-sign',
        '--mac-signing-key-user-name', project.findProperty('macSigning.keyUserName')
      ]
    }
  }
}

if (Os.isFamily(Os.FAMILY_WINDOWS)) {
  jlink {
    jpackage {
      icon = 'src/main/win/app.ico'
      imageName = 'helloworld'
      imageOptions = [
        '--copyright', copyright,
      ]
      installerName = 'helloworld'
      installerType = 'msi'
      installerOptions = [
        '--copyright', copyright,
        '--license-file', '../LICENSE.txt',
        '--win-dir-chooser',
        '--win-menu',
        '--win-menu-group', 'Muspellheim',
        '--win-upgrade-uuid', '1f42ed58-4e2b-49e8-bc39-f333396ff8b6'
      ]
    }
  }
}

gluonClient {
  resourcesList = ['app.properties']
  reflectionList = [
    'de.muspellheim.helloworld.frontend.MainViewController',
    'de.muspellheim.helloworld.frontend.InfoViewController',
    'javafx.geometry.Insets',
    'javafx.scene.control.Label',
    'javafx.scene.control.MenuButton',
    'javafx.scene.control.MenuItem',
    'javafx.scene.image.Image',
    'javafx.scene.image.ImageView',
    'javafx.scene.layout.HBox',
    'javafx.scene.layout.VBox',
    'javafx.scene.layout.StackPane',
  ]
}
